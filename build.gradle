group "com.pro.jenova"

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "gradle.plugin.com.palantir.gradle.gitversion:gradle-git-version:0.7.1"
    }
}

apply plugin: "com.palantir.git-version"

def resolvedVersion = resolveVersion()

subprojects {
    version resolvedVersion

    apply plugin: "java"
    apply plugin: "maven"

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    dependencies {
        testCompile "junit:junit:${junitVersion}"
        testCompile "org.springframework.boot:spring-boot-starter-test:${springBootVersion}"
    }

    repositories {
        maven {
            url "${nexusUrl}/repository/maven-public/"
        }
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                repository(url: "${nexusUrl}/repository/maven-releases/") {
                    authentication(userName: nexusUsername, password: nexusPassword)
                }
                snapshotRepository(url: "${nexusUrl}/repository/maven-snapshots/") {
                    authentication(userName: nexusUsername, password: nexusPassword)
                }
            }
        }
    }
}

def resolveVersion() {
    def dateAndTime = new Date().format("yyyyMMddHHmmss");

    def details = versionDetails()
    def branchName = details.branchName
    def commitHash = details.gitHash

    if (branchName.startsWith("release/")) {
        return branchName.substring("release/".length())
    }

    return branchName.concat("-").concat(dateAndTime).concat("-").concat(commitHash).concat("-SNAPSHOT")
}