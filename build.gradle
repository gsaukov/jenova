group "com.pro.jenova"

def resolvedVersion = project.hasProperty("versionToUse") ? project.versionToUse : "0.0.0"

subprojects {
    version resolvedVersion

    apply plugin: "java"
    apply plugin: "maven"

    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:deprecation"
        }
    }
}

task printVersion {
    doLast {
        println createVersion()
    }
}

def createVersion() {
    def branchName = "git rev-parse --abbrev-ref HEAD".execute().getText().trim()
    def commitHash = "git rev-parse --short HEAD".execute().getText().trim()
    def dateAndTime = new Date().format("yyyyMMddHHmmss")

    if (branchName.startsWith("release/")) {
        return branchName.substring("release/".length())
    }

    return dateAndTime.concat(".").concat(branchName).concat(".").concat(commitHash)
}