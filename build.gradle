group "com.pro.jenova"

def resolvedVersion = resolveVersion()

subprojects {
    version resolvedVersion

    apply plugin: "java"
    apply plugin: "maven"

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    dependencies {
        testCompile "junit:junit:${junitVersion}"
        testCompile "org.springframework.boot:spring-boot-starter-test:${springBootVersion}"
    }
}

task printVersion {
    doLast {
        println resolvedVersion
    }
}

def resolveVersion() {
    def branchName = "git rev-parse --abbrev-ref HEAD".execute().getText().trim()
    def commitHash = "git rev-parse --short HEAD".execute().getText().trim()
    def dateAndTime = new Date().format("yyyyMMddHHmmss")

    if (branchName.startsWith("release/")) {
        return branchName.substring("release/".length())
    }

    return branchName.concat("-").concat(dateAndTime).concat("-").concat(commitHash)
}