group "com.pro.jenova"

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "com.netflix.nebula:gradle-ospackage-plugin:${osPackageVersion}"
    }
}

apply plugin: "org.springframework.boot"
apply plugin: "io.spring.dependency-management"
apply plugin: "nebula.ospackage"

bootJar {
    mainClassName = "com.pro.jenova.omnidrive.Application"
    launchScript()
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

dependencies {
    compile project(':common')

    compile "org.springframework.boot:spring-boot-starter-actuator"
    compile "org.springframework.cloud:spring-cloud-starter-netflix-eureka-client"
    compile "org.springframework.cloud:spring-cloud-starter-openfeign"
    compile "org.springframework.boot:spring-boot-starter-web"
    compile "org.springframework.boot:spring-boot-starter-data-jpa"
    compile "org.springframework.boot:spring-boot-starter-amqp"

    runtime "net.logstash.logback:logstash-logback-encoder:${logstashEncoderVersion}"
    runtime "org.liquibase:liquibase-core:${liquibaseVersion}"
    runtime "mysql:mysql-connector-java:${mysqlVersion}"
    runtime "com.h2database:h2:${h2Version}"
}

repositories {
    maven {
        url "https://repo.spring.io/libs-milestone"
    }
}

ospackage {
    packageName = project.name
    version = project.version
    os = LINUX
    type = BINARY

    into "/opt/omnidrive"
    user "omnidrive"
    permissionGroup "omnidrive"

    from("${rootDir}/omnidrive/build/libs") {
        into "application"
        include "**/*.jar"
        fileMode 0775
    }

    from("${rootDir}/omnidrive/build/resources/main") {
        into "configuration"
        include "**/*.conf"
        fileMode 0775
    }

    link("/etc/init.d/omnidrive", "/opt/omnidrive/application/omnidrive-${project.version}.jar")
    link("/opt/omnidrive/application/omnidrive-${project.version}.conf", "/opt/omnidrive/configuration/startup.conf")
}