group "com.pro.jenova"

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "com.netflix.nebula:gradle-ospackage-plugin:${osPackageVersion}"
    }
}

apply plugin: "org.springframework.boot"
apply plugin: "io.spring.dependency-management"
apply plugin: "nebula.ospackage"

bootRepackage {
    mainClass = "com.pro.jenova.serenity.Application"
    executable = true
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

dependencies {
    compile "org.springframework.boot:spring-boot-starter-actuator"
    compile "org.springframework.cloud:spring-cloud-config-server"
    compile "org.springframework.cloud:spring-cloud-starter-netflix-eureka-client"
}

repositories {
    maven {
        url "https://repo.spring.io/libs-milestone"
    }
}

ospackage {
    packageName = project.name
    version = project.version
    os = LINUX
    type = BINARY

    into "/opt/serenity"
    user "serenity"
    permissionGroup "serenity"

    from("${rootDir}/serenity/build/libs") {
        into "application"
        include "**/*.jar"
        fileMode 0755
    }

    link("/etc/init.d/serenity", "/opt/serenity/application/serenity-${project.version}.jar")
}

